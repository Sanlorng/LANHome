apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--all', '--count'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    }
    catch (ignored) {
        return -1
    }
}
def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tag', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return "in Develop"
    }
}

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            keyAlias KEY_ALIAS
            keyPassword KEY_PASS
            storeFile file(KEY_STORE_FILE)
            storePassword STORE_PASS
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    compileSdkVersion 29
    buildToolsVersion '29.0.0'
    defaultConfig {
        applicationId "com.ycz.lanhome"
        minSdkVersion 26
        targetSdkVersion 29
        versionCode getVersionCode()
        versionName getVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilter("arm64-v8a")
            abiFilter("armeabi-v7a")
            abiFilter("x86")
            abiFilter("x86_64")
        }
    }
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            def buildTypeName = variant.buildType.name
            def fileName = "${defaultConfig.applicationId}_${defaultConfig.versionName}_${defaultConfig.versionCode}_${buildTypeName}.apk"
            outputFileName = fileName

        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.release
        }
    }
}
final String navigation_ktx_version = "2.1.0-alpha05"
final String room_version = "2.1.0"
final String lifecycle_version = "2.2.0-alpha01"
final String glide_version = "4.9.0"
final String ok_http_version = '3.14.0'
final String retrofit_version = "2.6.0"
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //Kotlin Library
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1"
    //Glide Library
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.0.0'
    kapt "com.github.bumptech.glide:compiler:$glide_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    //Android X Library
    implementation "androidx.preference:preference:1.1.0-beta01"
    implementation 'androidx.appcompat:appcompat:1.1.0-beta01'
    implementation 'androidx.viewpager:viewpager:1.0.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0-alpha05'
    implementation "androidx.drawerlayout:drawerlayout:1.1.0-alpha02"
    implementation 'androidx.core:core-ktx:1.2.0-alpha02'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    //Material Design Components Library
    implementation 'com.google.android.material:material:1.1.0-alpha07'
    //Retrofit Library
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    //OkHttp Library
    implementation "com.squareup.okhttp3:okhttp:$ok_http_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$ok_http_version"
    //Android Jetpack - Navigation Component Library
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_ktx_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_ktx_version"
    //Android Jetpack - Lifecycle Component Library
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"
    //Android Jetpack - WorkManager Component Library
    implementation "androidx.work:work-runtime-ktx:2.1.0-beta02"
    //Android Jetpack - Room Component Library
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    //Chart
//    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0-alpha'
    //Test Library
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation project(path: ':MPChartLib-debug')
    implementation 'ywl.ywl5320:wlmedia:1.0.0-beta17'


}
